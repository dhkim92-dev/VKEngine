#version 450
layout (local_size_x = 1, local_size_y = 1, local_size_z = 1)in;
layout(std430, binding = 0) readonly buffer Volume{
	float raw[];
};
layout(std430, binding = 1) readonly buffer EdgePsum{
	uint edge_psum[];
};
layout(std430, binding = 2) readonly buffer EdgeCompact{
	uint edge_compact[];
};
layout(std430, binding = 3) readonly buffer Dim{
	uvec3 dim;
};
layout(binding=4) uniform Isovalue{
	float isovalue;
};
layout(std430, binding = 5) writeonly buffer Vertices{
	float vertices[];
};

uint pos2idx(uvec3 pos, uvec3 dim){
	return pos.x + pos.y*dim.x + pos.z*dim.x*dim.y;
}

uvec3 idx2pos(uint idx, uvec3 dim){
	uvec3 ret;
	ret.z = idx / (dim.x*dim.y);
	ret.y = (idx - ret.z * dim.x * dim.y) / dim.x;
	ret.x = idx % dim.x;
	return ret;
}

void main(){
	uint tid = gl_GlobalInvocationID.x;
	uint edge_id = edge_compact[tid*3];
	uint dir = edge_id%3;
	uint vid = uint(edge_id/3);
	uvec3 edim = dim - uvec3(1,1,1);
	uvec3 p0 = idx2pos(vid, edim);
	uvec3 p1;
	p1.z = (0x02 & dir) >> 1;
	p1.y = (0x01 & dir);
	p1.x = uint(0x00==dir);
	p1 += p0;
	float v0 = raw[pos2idx(p0, dim)];
	float v1 = raw[pos2idx(p1, dim)];
	vec3 ip= p0 + (p1 - p0) * ((isovalue - v0)/(v1-v0));
	vertices[tid*3] = ip.x / float(edim.x+1) ;
	vertices[tid*3+1] = ip.y / float(edim.y + 1);
	vertices[tid*3+2] = ip.z / float(edim.z + 1);
}